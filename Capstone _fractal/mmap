SECTIONS
{
    . = 0x80000;
    .text : { 
        KEEP(*(.text.boot)) *(.text .text.* .gnu.linkonce.t*) 
    }
    .rodata : { 
        *(.rodata .rodata.* .gnu.linkonce.r*) 
    }
    . = ALIGN(4096);
    PROVIDE(_data = .);
    .data : { 
        *(.data .data.* .gnu.linkonce.d*) 
    }
    .bss (NOLOAD) : {
        . = ALIGN(16);
        __bss_start = .;
        *(.bss .bss.*)
        *(COMMON)
        __bss_end = .;
    }
    .stack_core0 : {
        . = ALIGN(16);				
        __stack_start_core0__ = .;
        . = . + 4MB;
        __stack_end_core0__ = .;    
    }
	.stack_core1 : {
        . = ALIGN(16);				
        __stack_start_core1__ = .;
        . = . + 4MB;
        __stack_end_core1__ = .;     
    }
	.stack_core2 :	{
        . = ALIGN(16);				
        __stack_start_core2__ = .;
        . = . + 4MB;
        __stack_end_core2__ = .; 
    }
	.stack_core3 :	{
        . = ALIGN(16);				
        __stack_start_core3__ = .;
        . = . + 4MB;
        __stack_end_core3__ = .; 
    }
    . = ALIGN(4096);
    __heap_start = .;
    .heap :	{
     	. = ALIGN(4096);
     	__heap_start__ = .;
        . = . + 2MB;
        __heap_end = .;		
    	_end = .; PROVIDE (end = .);
	}
   /DISCARD/ : { *(.comment) *(.gnu*) *(.note*) *(.eh_frame*) }
}
